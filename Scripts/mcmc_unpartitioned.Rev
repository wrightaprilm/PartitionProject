################################################################################
#
# RevBayes Example: Inferring a Phylogeny of Fossil Bears Using the Mk Model
# 
# This file: Runs the full MCMC ...
#
# authors:  April M. Wright, Michael J. Landis
#
################################################################################

#######################
# Reading in the Data #
#######################
# Import the morphological character matrix #
# this file contains only the taxa for which morphological characters are available #
filenames <- v("data/binary/bin", "data/trinary/trin")

n_data_subsets <- filenames.size()
for (i in 1:n_data_subsets) {
    data[i] = readDiscreteCharacterData(filenames[i])
}


## helpers
mvi = 1
mni = 1
taxa <- data[1].names()
n_taxa <- taxa.size()
n_branches <- 2 * n_taxa - 3

# Load the model files
source("scripts/analytical_partititioned/model_simple.Rev") # Morphological character change model

########
# MCMC #
########

# initialize the model object #
mymodel = model(phylogeny)
# Create a vector of monitors #
# 1. for the full model #
monitors[mni++] = mnModel(filename="output/simple.log", printgen=10)
# 2. the tree #
monitors[mni++] = mnFile(filename="output/simple.trees", printgen=10, phylogeny)
# 3. and a few select parameters to be printed to the screen #
monitors[mni++] = mnScreen(printgen=100)

# Initialize the MCMC object #
mymcmc = mcmc(mymodel, monitors, moves)

# Run the MCMC #
mymcmc.run(generations=1000000)

# Read in the tree trace and construct the consensus tree tree #
trace = readTreeTrace("output/simple.trees", treetype="non-clock")
trace.setBurnin(0.25)

# Summarize tree trace and the consensus tree to file
mapTree(trace, file="output/simple.map.tre")
consensusTree(trace, file="output/simple.majrule.tre")

# Quit RevBayes #
q()
